C***,Calculate section properties for U-100-4 using various mesh sizes
C***,Store results for each analysis in parameter arrays
*del,all
size=10
*dim,el_sizes,array,size
*dim,iws,array,size
*dim,prep_times,array,size
*dim,solve_times,array,size
*dim,node_counts,array,size
*dim,element_counts,array,size
*dim,latex_lines,string,60,size
*dim,la,string,8
*dim,lb,string,8
*dim,lc,string,24
*set,la(1),'Ansys(8'
*set,lb(1),') &'
*set,lc(1),'\(10^{-12}\)\\'
*dim,cva,string,8
*dim,cvb,string,8
index=1
previous_node_count=0
*do,EL_SIZE,0.0145,0.0005,-0.0035
/go
*ask,EL_SIZE,Give 0 to end,%EL_SIZE%
*if,EL_SIZE,le,0,exit
PARSAV,all,ansys/u-100
/clear
PARRES,,ansys/u-100
/prep7
*GET,P1,ACTIVE,,TIME,CPU
RECTNG,0.008,0.1/2,0,0.004
RECTNG,0,0.004,0.008,0.1/2
CYL4,0.008,0.008,0.004,180,0.008,270
/rep, fast
AGEN,1,ALL,,,0,-0.1/2,,,1,1
ARSYM,Y,ALL
/rep, fast
AGLUE,ALL
LESIZE,ALL,EL_SIZE
ET,1,PLANE183
AMESH,ALL
*get,node_count,node,all,count
! skip analysis if mesh is similar as previous
*if,node_count,eq,previous_node_count,cycle
previous_node_count=node_count
*get,element_count,element,all,count
secwrite,ansys/u,,,1
*GET,P2,ACTIVE,,TIME,CPU
SECTYPE,1,BEAM,MESH
SECREAD,ansys/u,,,MESH
*GET,P3,ACTIVE,,TIME,CPU
SECPLOT,1,1
*get,iw,secp,1,prop,warp
el_sizes(index)=el_size
prep_times(index)=p2-p1
solve_times(index)=p3-p2
iws(index)=iw
node_counts(index)=node_count
element_counts(index)=element_count
*set,cva(1),chrval(el_size)
*set,cvb(2),chrval(nint(iw*1.e12))
latex_lines(1,index)='%la(1)%,%cva(1)%,%node_count%,%element_count%%lb(1)% %cvb(1)% %lc(1)%'
index=index+1
*enddo
PARSAV,all,ansys/u-100